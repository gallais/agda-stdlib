<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Container.FreeMonad</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- The free monad construction on containers</a>
<a id="151" class="Comment">------------------------------------------------------------------------</a>

<a id="225" class="Symbol">{-#</a> <a id="229" class="Keyword">OPTIONS</a> <a id="237" class="Pragma">--without-K</a> <a id="249" class="Pragma">--sized-types</a> <a id="263" class="Symbol">#-}</a>

<a id="268" class="Keyword">module</a> <a id="275" href="Data.Container.FreeMonad.html" class="Module">Data.Container.FreeMonad</a> <a id="300" class="Keyword">where</a>

<a id="307" class="Keyword">open</a> <a id="312" class="Keyword">import</a> <a id="319" href="Level.html" class="Module">Level</a>
<a id="325" class="Keyword">open</a> <a id="330" class="Keyword">import</a> <a id="337" href="Data.Sum.Base.html" class="Module">Data.Sum.Base</a> <a id="351" class="Keyword">using</a> <a id="357" class="Symbol">(</a><a id="358" href="Data.Sum.Base.html#784" class="InductiveConstructor">inj₁</a><a id="362" class="Symbol">;</a> <a id="364" href="Data.Sum.Base.html#809" class="InductiveConstructor">inj₂</a> <a id="369" class="Symbol">;</a> <a id="371" href="Data.Sum.Base.html#1089" class="Function Operator">[_,_]′</a><a id="377" class="Symbol">)</a>
<a id="379" class="Keyword">open</a> <a id="384" class="Keyword">import</a> <a id="391" href="Data.Product.html" class="Module">Data.Product</a>
<a id="404" class="Keyword">open</a> <a id="409" class="Keyword">import</a> <a id="416" href="Data.Container.html" class="Module">Data.Container</a>
<a id="431" class="Keyword">open</a> <a id="436" class="Keyword">import</a> <a id="443" href="Data.Container.Combinator.html" class="Module">Data.Container.Combinator</a> <a id="469" class="Keyword">using</a> <a id="475" class="Symbol">(</a><a id="476" href="Data.Container.Combinator.html#835" class="Function">const</a><a id="481" class="Symbol">;</a> <a id="483" href="Data.Container.Combinator.html#1961" class="Function Operator">_⊎_</a><a id="486" class="Symbol">)</a>
<a id="488" class="Keyword">open</a> <a id="493" class="Keyword">import</a> <a id="500" href="Data.W.html" class="Module">Data.W</a>
<a id="507" class="Keyword">open</a> <a id="512" class="Keyword">import</a> <a id="519" href="Category.Monad.html" class="Module">Category.Monad</a>

<a id="535" class="Keyword">infixl</a> <a id="542" class="Number">1</a> <a id="544" href="Data.Container.FreeMonad.html#1432" class="Function Operator">_⋆C_</a>
<a id="549" class="Keyword">infix</a>  <a id="556" class="Number">1</a> <a id="558" href="Data.Container.FreeMonad.html#1517" class="Function Operator">_⋆_</a>

<a id="563" class="Comment">------------------------------------------------------------------------</a>
<a id="636" class="Comment">-- The free monad construction over a container and a set is, in</a>
<a id="701" class="Comment">-- universal algebra terminology, also known as the term algebra over a</a>
<a id="773" class="Comment">-- signature (a container) and a set (of variable symbols). The return</a>
<a id="844" class="Comment">-- of the free monad corresponds to variables and the bind operator</a>
<a id="912" class="Comment">-- corresponds to (parallel) substitution.</a>

<a id="956" class="Comment">-- A useful intuition is to think of containers describing single</a>
<a id="1022" class="Comment">-- operations and the free monad construction over a container and a set</a>
<a id="1095" class="Comment">-- describing a tree of operations as nodes and elements of the set as</a>
<a id="1166" class="Comment">-- leafs. If one starts at the root, then any path will pass finitely</a>
<a id="1236" class="Comment">-- many nodes (operations described by the container) and eventually end</a>
<a id="1309" class="Comment">-- up in a leaf (element of the set) -- hence the Kleene star notation</a>
<a id="1380" class="Comment">-- (the type can be read as a regular expression).</a>

<a id="_⋆C_"></a><a id="1432" href="Data.Container.FreeMonad.html#1432" class="Function Operator">_⋆C_</a> <a id="1437" class="Symbol">:</a> <a id="1439" class="Symbol">∀</a> <a id="1441" class="Symbol">{</a><a id="1442" href="Data.Container.FreeMonad.html#1442" class="Bound">x</a> <a id="1444" href="Data.Container.FreeMonad.html#1444" class="Bound">s</a> <a id="1446" href="Data.Container.FreeMonad.html#1446" class="Bound">p</a><a id="1447" class="Symbol">}</a> <a id="1449" class="Symbol">→</a> <a id="1451" href="Data.Container.Core.html#558" class="Record">Container</a> <a id="1461" href="Data.Container.FreeMonad.html#1444" class="Bound">s</a> <a id="1463" href="Data.Container.FreeMonad.html#1446" class="Bound">p</a> <a id="1465" class="Symbol">→</a> <a id="1467" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="1471" href="Data.Container.FreeMonad.html#1442" class="Bound">x</a> <a id="1473" class="Symbol">→</a> <a id="1475" href="Data.Container.Core.html#558" class="Record">Container</a> <a id="1485" class="Symbol">(</a><a id="1486" href="Data.Container.FreeMonad.html#1444" class="Bound">s</a> <a id="1488" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="1490" href="Data.Container.FreeMonad.html#1442" class="Bound">x</a><a id="1491" class="Symbol">)</a> <a id="1493" href="Data.Container.FreeMonad.html#1446" class="Bound">p</a>
<a id="1495" href="Data.Container.FreeMonad.html#1495" class="Bound">C</a> <a id="1497" href="Data.Container.FreeMonad.html#1432" class="Function Operator">⋆C</a> <a id="1500" href="Data.Container.FreeMonad.html#1500" class="Bound">X</a> <a id="1502" class="Symbol">=</a> <a id="1504" href="Data.Container.Combinator.html#835" class="Function">const</a> <a id="1510" href="Data.Container.FreeMonad.html#1500" class="Bound">X</a> <a id="1512" href="Data.Container.Combinator.html#1961" class="Function Operator">⊎</a> <a id="1514" href="Data.Container.FreeMonad.html#1495" class="Bound">C</a>

<a id="_⋆_"></a><a id="1517" href="Data.Container.FreeMonad.html#1517" class="Function Operator">_⋆_</a> <a id="1521" class="Symbol">:</a> <a id="1523" class="Symbol">∀</a> <a id="1525" class="Symbol">{</a><a id="1526" href="Data.Container.FreeMonad.html#1526" class="Bound">x</a> <a id="1528" href="Data.Container.FreeMonad.html#1528" class="Bound">s</a> <a id="1530" href="Data.Container.FreeMonad.html#1530" class="Bound">p</a><a id="1531" class="Symbol">}</a> <a id="1533" class="Symbol">→</a> <a id="1535" href="Data.Container.Core.html#558" class="Record">Container</a> <a id="1545" href="Data.Container.FreeMonad.html#1528" class="Bound">s</a> <a id="1547" href="Data.Container.FreeMonad.html#1530" class="Bound">p</a> <a id="1549" class="Symbol">→</a> <a id="1551" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="1555" href="Data.Container.FreeMonad.html#1526" class="Bound">x</a> <a id="1557" class="Symbol">→</a> <a id="1559" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="1563" class="Symbol">(</a><a id="1564" href="Data.Container.FreeMonad.html#1526" class="Bound">x</a> <a id="1566" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="1568" href="Data.Container.FreeMonad.html#1528" class="Bound">s</a> <a id="1570" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="1572" href="Data.Container.FreeMonad.html#1530" class="Bound">p</a><a id="1573" class="Symbol">)</a>
<a id="1575" href="Data.Container.FreeMonad.html#1575" class="Bound">C</a> <a id="1577" href="Data.Container.FreeMonad.html#1517" class="Function Operator">⋆</a> <a id="1579" href="Data.Container.FreeMonad.html#1579" class="Bound">X</a> <a id="1581" class="Symbol">=</a> <a id="1583" href="Data.Container.html#1539" class="Function">μ</a> <a id="1585" class="Symbol">(</a><a id="1586" href="Data.Container.FreeMonad.html#1575" class="Bound">C</a> <a id="1588" href="Data.Container.FreeMonad.html#1432" class="Function Operator">⋆C</a> <a id="1591" href="Data.Container.FreeMonad.html#1579" class="Bound">X</a><a id="1592" class="Symbol">)</a>

<a id="1595" class="Keyword">module</a> <a id="1602" href="Data.Container.FreeMonad.html#1602" class="Module">_</a> <a id="1604" class="Symbol">{</a><a id="1605" href="Data.Container.FreeMonad.html#1605" class="Bound">s</a> <a id="1607" href="Data.Container.FreeMonad.html#1607" class="Bound">p</a><a id="1608" class="Symbol">}</a> <a id="1610" class="Symbol">{</a><a id="1611" href="Data.Container.FreeMonad.html#1611" class="Bound">C</a> <a id="1613" class="Symbol">:</a> <a id="1615" href="Data.Container.Core.html#558" class="Record">Container</a> <a id="1625" href="Data.Container.FreeMonad.html#1605" class="Bound">s</a> <a id="1627" href="Data.Container.FreeMonad.html#1607" class="Bound">p</a><a id="1628" class="Symbol">}</a> <a id="1630" class="Keyword">where</a>

  <a id="1639" href="Data.Container.FreeMonad.html#1639" class="Function">inn</a> <a id="1643" class="Symbol">:</a> <a id="1645" class="Symbol">∀</a> <a id="1647" class="Symbol">{</a><a id="1648" href="Data.Container.FreeMonad.html#1648" class="Bound">x</a><a id="1649" class="Symbol">}</a> <a id="1651" class="Symbol">{</a><a id="1652" href="Data.Container.FreeMonad.html#1652" class="Bound">X</a> <a id="1654" class="Symbol">:</a> <a id="1656" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="1660" href="Data.Container.FreeMonad.html#1648" class="Bound">x</a><a id="1661" class="Symbol">}</a> <a id="1663" class="Symbol">→</a> <a id="1665" href="Data.Container.Core.html#755" class="Function Operator">⟦</a> <a id="1667" href="Data.Container.FreeMonad.html#1611" class="Bound">C</a> <a id="1669" href="Data.Container.Core.html#755" class="Function Operator">⟧</a> <a id="1671" class="Symbol">(</a><a id="1672" href="Data.Container.FreeMonad.html#1611" class="Bound">C</a> <a id="1674" href="Data.Container.FreeMonad.html#1517" class="Function Operator">⋆</a> <a id="1676" href="Data.Container.FreeMonad.html#1652" class="Bound">X</a><a id="1677" class="Symbol">)</a> <a id="1679" class="Symbol">→</a> <a id="1681" href="Data.Container.FreeMonad.html#1611" class="Bound">C</a> <a id="1683" href="Data.Container.FreeMonad.html#1517" class="Function Operator">⋆</a> <a id="1685" href="Data.Container.FreeMonad.html#1652" class="Bound">X</a>
  <a id="1689" href="Data.Container.FreeMonad.html#1639" class="Function">inn</a> <a id="1693" class="Symbol">(</a><a id="1694" href="Data.Container.FreeMonad.html#1694" class="Bound">s</a> <a id="1696" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1698" href="Data.Container.FreeMonad.html#1698" class="Bound">f</a><a id="1699" class="Symbol">)</a> <a id="1701" class="Symbol">=</a> <a id="1703" href="Data.W.html#577" class="InductiveConstructor">sup</a> <a id="1707" class="Symbol">(</a><a id="1708" href="Data.Sum.Base.html#809" class="InductiveConstructor">inj₂</a> <a id="1713" href="Data.Container.FreeMonad.html#1694" class="Bound">s</a> <a id="1715" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1717" href="Data.Container.FreeMonad.html#1698" class="Bound">f</a><a id="1718" class="Symbol">)</a>

  <a id="1723" href="Data.Container.FreeMonad.html#1723" class="Function">rawMonad</a> <a id="1732" class="Symbol">:</a> <a id="1734" class="Symbol">∀</a> <a id="1736" class="Symbol">{</a><a id="1737" href="Data.Container.FreeMonad.html#1737" class="Bound">x</a><a id="1738" class="Symbol">}</a> <a id="1740" class="Symbol">→</a> <a id="1742" href="Category.Monad.html#448" class="Function">RawMonad</a> <a id="1751" class="Symbol">{</a><a id="1752" href="Data.Container.FreeMonad.html#1605" class="Bound">s</a> <a id="1754" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="1756" href="Data.Container.FreeMonad.html#1607" class="Bound">p</a> <a id="1758" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="1760" href="Data.Container.FreeMonad.html#1737" class="Bound">x</a><a id="1761" class="Symbol">}</a> <a id="1763" class="Symbol">(</a><a id="1764" href="Data.Container.FreeMonad.html#1611" class="Bound">C</a> <a id="1766" href="Data.Container.FreeMonad.html#1517" class="Function Operator">⋆_</a><a id="1768" class="Symbol">)</a>
  <a id="1772" href="Data.Container.FreeMonad.html#1723" class="Function">rawMonad</a> <a id="1781" class="Symbol">=</a> <a id="1783" class="Keyword">record</a> <a id="1790" class="Symbol">{</a> <a id="1792" href="Category.Monad.Indexed.html#644" class="Field">return</a> <a id="1799" class="Symbol">=</a> <a id="1801" href="Data.Container.FreeMonad.html#1839" class="Function">return</a><a id="1807" class="Symbol">;</a> <a id="1809" href="Category.Monad.Indexed.html#677" class="Field Operator">_&gt;&gt;=_</a> <a id="1815" class="Symbol">=</a> <a id="1817" href="Data.Container.FreeMonad.html#1906" class="Function Operator">_&gt;&gt;=_</a> <a id="1823" class="Symbol">}</a>
    <a id="1829" class="Keyword">where</a>
    <a id="1839" href="Data.Container.FreeMonad.html#1839" class="Function">return</a> <a id="1846" class="Symbol">:</a> <a id="1848" class="Symbol">∀</a> <a id="1850" class="Symbol">{</a><a id="1851" href="Data.Container.FreeMonad.html#1851" class="Bound">X</a><a id="1852" class="Symbol">}</a> <a id="1854" class="Symbol">→</a> <a id="1856" href="Data.Container.FreeMonad.html#1851" class="Bound">X</a> <a id="1858" class="Symbol">→</a> <a id="1860" href="Data.Container.FreeMonad.html#1611" class="Bound">C</a> <a id="1862" href="Data.Container.FreeMonad.html#1517" class="Function Operator">⋆</a> <a id="1864" href="Data.Container.FreeMonad.html#1851" class="Bound">X</a>
    <a id="1870" href="Data.Container.FreeMonad.html#1839" class="Function">return</a> <a id="1877" href="Data.Container.FreeMonad.html#1877" class="Bound">x</a> <a id="1879" class="Symbol">=</a> <a id="1881" href="Data.W.html#577" class="InductiveConstructor">sup</a> <a id="1885" class="Symbol">(</a><a id="1886" href="Data.Sum.Base.html#784" class="InductiveConstructor">inj₁</a> <a id="1891" href="Data.Container.FreeMonad.html#1877" class="Bound">x</a> <a id="1893" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1895" class="Symbol">λ</a> <a id="1897" class="Symbol">())</a>

    <a id="1906" href="Data.Container.FreeMonad.html#1906" class="Function Operator">_&gt;&gt;=_</a> <a id="1912" class="Symbol">:</a> <a id="1914" class="Symbol">∀</a> <a id="1916" class="Symbol">{</a><a id="1917" href="Data.Container.FreeMonad.html#1917" class="Bound">X</a> <a id="1919" href="Data.Container.FreeMonad.html#1919" class="Bound">Y</a><a id="1920" class="Symbol">}</a> <a id="1922" class="Symbol">→</a> <a id="1924" href="Data.Container.FreeMonad.html#1611" class="Bound">C</a> <a id="1926" href="Data.Container.FreeMonad.html#1517" class="Function Operator">⋆</a> <a id="1928" href="Data.Container.FreeMonad.html#1917" class="Bound">X</a> <a id="1930" class="Symbol">→</a> <a id="1932" class="Symbol">(</a><a id="1933" href="Data.Container.FreeMonad.html#1917" class="Bound">X</a> <a id="1935" class="Symbol">→</a> <a id="1937" href="Data.Container.FreeMonad.html#1611" class="Bound">C</a> <a id="1939" href="Data.Container.FreeMonad.html#1517" class="Function Operator">⋆</a> <a id="1941" href="Data.Container.FreeMonad.html#1919" class="Bound">Y</a><a id="1942" class="Symbol">)</a> <a id="1944" class="Symbol">→</a> <a id="1946" href="Data.Container.FreeMonad.html#1611" class="Bound">C</a> <a id="1948" href="Data.Container.FreeMonad.html#1517" class="Function Operator">⋆</a> <a id="1950" href="Data.Container.FreeMonad.html#1919" class="Bound">Y</a>
    <a id="1956" href="Data.W.html#577" class="InductiveConstructor">sup</a> <a id="1960" class="Symbol">(</a><a id="1961" href="Data.Sum.Base.html#784" class="InductiveConstructor">inj₁</a> <a id="1966" href="Data.Container.FreeMonad.html#1966" class="Bound">x</a> <a id="1968" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1970" class="Symbol">_)</a> <a id="1973" href="Data.Container.FreeMonad.html#1906" class="Function Operator">&gt;&gt;=</a> <a id="1977" href="Data.Container.FreeMonad.html#1977" class="Bound">k</a> <a id="1979" class="Symbol">=</a> <a id="1981" href="Data.Container.FreeMonad.html#1977" class="Bound">k</a> <a id="1983" href="Data.Container.FreeMonad.html#1966" class="Bound">x</a>
    <a id="1989" href="Data.W.html#577" class="InductiveConstructor">sup</a> <a id="1993" class="Symbol">(</a><a id="1994" href="Data.Sum.Base.html#809" class="InductiveConstructor">inj₂</a> <a id="1999" href="Data.Container.FreeMonad.html#1999" class="Bound">s</a> <a id="2001" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2003" href="Data.Container.FreeMonad.html#2003" class="Bound">f</a><a id="2004" class="Symbol">)</a> <a id="2006" href="Data.Container.FreeMonad.html#1906" class="Function Operator">&gt;&gt;=</a> <a id="2010" href="Data.Container.FreeMonad.html#2010" class="Bound">k</a> <a id="2012" class="Symbol">=</a> <a id="2014" href="Data.Container.FreeMonad.html#1639" class="Function">inn</a> <a id="2018" class="Symbol">(</a><a id="2019" href="Data.Container.FreeMonad.html#1999" class="Bound">s</a> <a id="2021" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2023" class="Symbol">λ</a> <a id="2025" href="Data.Container.FreeMonad.html#2025" class="Bound">p</a> <a id="2027" class="Symbol">→</a> <a id="2029" href="Data.Container.FreeMonad.html#2003" class="Bound">f</a> <a id="2031" href="Data.Container.FreeMonad.html#2025" class="Bound">p</a> <a id="2033" href="Data.Container.FreeMonad.html#1906" class="Function Operator">&gt;&gt;=</a> <a id="2037" href="Data.Container.FreeMonad.html#2010" class="Bound">k</a><a id="2038" class="Symbol">)</a>
</pre></body></html>